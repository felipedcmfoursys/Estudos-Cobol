      *--------------------------------------------------------------*  00010004
       IDENTIFICATION                      DIVISION.                    00020004
      *--------------------------------------------------------------*  00030004
       PROGRAM-ID.                         P112NX05.                    00040008
       AUTHOR.                             NELSON.                      00050008
      *                                                                 00060004
      *--------------------------------------------------------------*  00070004
       ENVIRONMENT                         DIVISION.                    00080004
      *--------------------------------------------------------------*  00090004
       CONFIGURATION                       SECTION.                     00100004
       SPECIAL-NAMES.                                                   00110004
           DECIMAL-POINT IS COMMA.                                      00120004
      *
       INPUT-OUTPUT                        SECTION.
       FILE-CONTROL.
           SELECT EXTTDV25 ASSIGN          TO UT-S-EXTTDV25
           FILE STATUS IS FS-EXTTDV25
           .
           SELECT EMPR2025 ASSIGN          TO UT-S-EMPR2025
           FILE STATUS IS FS-EMPR2025
           .
      *--------------------------------------------------------------*  00130004
       DATA                                DIVISION.                    00140004
      *--------------------------------------------------------------*  00150004
       FILE                                SECTION.                     00160004
      *--------------------------------------------------------------*  00210008
       FD  EXTTDV25
           LABEL RECORD STANDARD
           BLOCK CONTAINS 0 RECORDS
           RECORDING MODE IS F
           RECORD CONTAINS 44 CHARACTERS
           DATA RECORD IS FD-EXTTDV25
           .
       01  FD-EXTTDV25                     PIC X(44).

       FD  EMPR2025
           LABEL RECORD STANDARD
           BLOCK CONTAINS 0 RECORDS
           RECORDING MODE IS F
           RECORD CONTAINS 72 CHARACTERS
           DATA RECORD IS FD-EMPR2025
           .
       01  FD-EMPR2025                     PIC X(72).

      *--------------------------------------------------------------*  00210008
       WORKING-STORAGE                     SECTION.                     00160004
      *--------------------------------------------------------------*  00210008
      * DECLARACAO DAS VARIAVEIS ESPELHO DOS ARQUIVOS
      *--------------------------------------------------------------*  00210008
           COPY B112DV25.
           COPY B112EM25.

      *--------------------------------------------------------------*  00210008
      * VARIAVEIS DE FILE STATUS
      *--------------------------------------------------------------*  00210008
           COPY VARERRO.

      *--------------------------------------------------------------*  00210008
      * VARIAVEIS DE FILE STATUS
      *--------------------------------------------------------------*  00210008
       01  FS-EXTTDV25                     PIC X(02).
           88 SUCESSO-DV25                         VALUE '00'.
           88 FIM-ARQ-DV25                         VALUE '10'.

       01  FS-EMPR2025                     PIC X(02).
           88 SUCESSO-EM25                         VALUE '00'.
           88 FIM-ARQ-EM25                         VALUE '10'.

      *--------------------------------------------------------------*  00210008
      * COLOCAR AS MENSAGEMS EM VARIAVEIS EH UMA BOA PRATICA
      *--------------------------------------------------------------*  00210008
       77  WS-MSG01                        PIC X(60)
                                                   VALUE
           'ERRO ABERTURA EXTTDV25'.
       77  WS-MSG02                        PIC X(60)
                                                   VALUE
           'ERRO ABERTURA EMPR2025'.
       77  WS-MSG03                        PIC X(60)
                                                   VALUE
           'ERRO LEITURA EXTTDV25'.
       77  WS-MSG04                        PIC X(60)
                                                   VALUE
           'ERRO GRAVACAO EMPR2025'.
       77  WS-MSG05                        PIC X(60)
                                                   VALUE
           'ERRO FECHAMENTO EXTTDV25'.
       77  WS-MSG06                        PIC X(60)
                                                   VALUE
           'ERRO FECHAMENTO EMPR2025'.

      *--------------------------------------------------------------*  00210008
      * VARIAVEIS DE TRABALHO
      *--------------------------------------------------------------*  00210008
       77  WS-CTLIDO                       PIC 9(04)
                                                   COMP.
       77  WS-CTGRAV                       PIC 9(04)
                                                   COMP.
       01  WS-DATA-SISTEMA.
           05 WS-ANO                       PIC X(02).
           05 WS-MES                       PIC X(02).
           05 WS-DIA                       PIC X(02).

       01  WS-DATA-PROCESSAMENTO.
           05 WS-DIA                       PIC X(02).
           05 FILLER                       PIC X(01)
                                                   VALUE '/'.
           05 WS-MES                       PIC X(02).
           05 FILLER                       PIC X(03)
                                                   VALUE '/20'.
           05 WS-ANO                       PIC X(02).

      *--------------------------------------------------------------*  00210008
      * VARIAVEIS COM MASCARA DE EDICAO (FORMATADAS)
      *--------------------------------------------------------------*  00210008
       77  WS-CTLIDO-F                     PIC ZZZ9.                    00210208
       77  WS-CTGRAV-F                     PIC ZZZ9.                    00210308
      *--------------------------------------------------------------*  00211007
       PROCEDURE                           DIVISION.                    00220004
      *--------------------------------------------------------------*  00230004
       0000-MAIN.
           PERFORM 1000-INICIALIZAR
           PERFORM 2000-PROCESSAR
                   UNTIL FIM-ARQ-DV25
           PERFORM 3000-TERMINO
           GOBACK
           .
      *
       1000-INICIALIZAR.
           ACCEPT WS-DATA-SISTEMA FROM DATE

           MOVE CORR WS-DATA-SISTEMA       TO WS-DATA-PROCESSAMENTO

           MOVE 0                          TO WS-CTLIDO
           MOVE 0                          TO WS-CTGRAV
      *
           OPEN INPUT EXTTDV25
      *
           IF NOT SUCESSO-DV25
              MOVE WS-MSG01                TO WS-MSG
              MOVE FS-EXTTDV25             TO WS-FS
              PERFORM 9000-ERRO
           END-IF

           OPEN OUTPUT EMPR2025

           IF NOT SUCESSO-EM25
              MOVE WS-MSG03                TO WS-MSG
              MOVE FS-EMPR2025             TO WS-FS
              PERFORM 9000-ERRO
           END-IF
      *
           PERFORM 1500-LER-EXTTDV25
           .
      *
       1500-LER-EXTTDV25.
           READ EXTTDV25 INTO REG-EXTTDV25

           IF SUCESSO-DV25
              ADD 1 TO WS-CTLIDO
           ELSE
              IF NOT FIM-ARQ-DV25
                 MOVE WS-MSG03             TO WS-MSG
                 MOVE FS-EXTTDV25          TO WS-FS
                 PERFORM 9000-ERRO
              END-IF
           END-IF
           .
      *
       2000-PROCESSAR.
           IF  B112DV25-SALDO >= -5000
               PERFORM 2100-NEGOCIACAO-1
           ELSE
               IF B112DV25-SALDO > -30000
                  PERFORM 2200-NEGOCIACAO-2
               ELSE
                  PERFORM 2300-NEGOCIACAO-3
               END-IF
           END-IF

           PERFORM 1500-LER-EXTTDV25
           .
      *
       2100-NEGOCIACAO-1.
           COMPUTE ACORDOVALORAVISTA = B112DV25-SALDO * 0,8
           COMPUTE VALORPARC = B112DV25-SALDO * 1,2 / 12
           COMPUTE JUROSACORDOVALORPARC = B112DV25-SALDO * 0,2
           COMPUTE VALORTOTALPARC = VALORPARC * 12
           PERFORM 2500-GRAVAR-EMPR2025
           .
      *
       2200-NEGOCIACAO-2.
           COMPUTE ACORDOVALORAVISTA = B112DV25-SALDO * 0,6
           COMPUTE VALORPARC = B112DV25-SALDO * 1,4 / 36
           COMPUTE JUROSACORDOVALORPARC = B112DV25-SALDO * 0,4
           COMPUTE VALORTOTALPARC = VALORPARC * 36
           PERFORM 2500-GRAVAR-EMPR2025
           .
      *
       2300-NEGOCIACAO-3.
           COMPUTE ACORDOVALORAVISTA = B112DV25-SALDO * 0,5
           COMPUTE VALORPARC = B112DV25-SALDO * 1,5 / 72
           COMPUTE JUROSACORDOVALORPARC = B112DV25-SALDO * 0,5
           COMPUTE VALORTOTALPARC = VALORPARC * 72
           PERFORM 2500-GRAVAR-EMPR2025
           .
      *
       2500-GRAVAR-EMPR2025.
           MOVE B112DV25-CPF               TO CPF-EMPR2025
           MOVE B112DV25-NOME              TO NOME-EMPR2025
           MOVE WS-DATA-PROCESSAMENTO      TO DATA-EMPR2025
           MOVE B112DV25-SALDO             TO SALDO-EMPR2025

           WRITE FD-EMPR2025 FROM REG-EMPR2025

           IF NOT SUCESSO-EM25
              MOVE WS-MSG04                TO WS-MSG
              MOVE FS-EMPR2025             TO WS-FS
              PERFORM 9000-ERRO
           END-IF

           ADD 1                           TO WS-CTGRAV
           .
      *
       3000-TERMINO.
           MOVE WS-CTLIDO                  TO WS-CTLIDO-F
           MOVE WS-CTGRAV                  TO WS-CTGRAV-F

      *
      *                      1         2         3         4         5
      *             12345678901234567890123456789012345678901234567890
           DISPLAY "=================================================="
           DISPLAY "TOTAL DE REGISTROS LIDOS.........: "
                                           WS-CTLIDO-F
           DISPLAY "TOTAL DE REGISTROS GRAVADOS......: "
                                           WS-CTGRAV-F
           DISPLAY "=================================================="
      *
           CLOSE EXTTDV25
      *
           IF NOT SUCESSO-DV25
              MOVE WS-MSG05                TO WS-MSG
              MOVE FS-EXTTDV25             TO WS-FS
              PERFORM 9000-ERRO
           END-IF

      *
           CLOSE EMPR2025
      *
           IF NOT SUCESSO-EM25
              MOVE WS-MSG06                TO WS-MSG
              MOVE FS-EMPR2025             TO WS-FS
              PERFORM 9000-ERRO
           END-IF

           DISPLAY "=================================================="
           DISPLAY "==          TERMINO NORMAL DO P112NX05          =="
           DISPLAY "=================================================="
           .
           COPY ROTERRO.
