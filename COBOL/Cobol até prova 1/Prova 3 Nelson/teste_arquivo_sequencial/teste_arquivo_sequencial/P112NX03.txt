      *--------------------------------------------------------------*  00010004
       IDENTIFICATION                      DIVISION.                    00020004
      *--------------------------------------------------------------*  00030004
       PROGRAM-ID.                         P112NX03.                    00040008
       AUTHOR.                             NELSON.                      00050008
      *                                                                 00060004
      *--------------------------------------------------------------*  00070004
       ENVIRONMENT                         DIVISION.                    00080004
      *--------------------------------------------------------------*  00090004
       CONFIGURATION                       SECTION.                     00100004
       SPECIAL-NAMES.                                                   00110004
           DECIMAL-POINT IS COMMA.                                      00120004
      *
       INPUT-OUTPUT                        SECTION.
       FILE-CONTROL.
           SELECT EXTTA025 ASSIGN          TO UT-S-EXTTA025
           FILE STATUS IS FS-EXTTA025
           .
           SELECT EXTTCR25 ASSIGN          TO UT-S-EXTTCR25
           FILE STATUS IS FS-EXTTCR25
           .
           SELECT EXTTDV25 ASSIGN          TO UT-S-EXTTDV25
           FILE STATUS IS FS-EXTTDV25
           .
      *--------------------------------------------------------------*  00130004
       DATA                                DIVISION.                    00140004
      *--------------------------------------------------------------*  00150004
       FILE                                SECTION.                     00160004
      *--------------------------------------------------------------*  00210008
       FD  EXTTA025
           LABEL RECORD STANDARD
           BLOCK CONTAINS 0 RECORDS
           RECORDING MODE IS F
           RECORD CONTAINS 44 CHARACTERS
           DATA RECORD IS FD-EXTTA025
           .
       01  FD-EXTTA025                     PIC X(44).

       FD  EXTTCR25
           LABEL RECORD STANDARD
           BLOCK CONTAINS 0 RECORDS
           RECORDING MODE IS F
           RECORD CONTAINS 44 CHARACTERS
           DATA RECORD IS FD-EXTTCR25
           .
       01  FD-EXTTCR25                     PIC X(44).

       FD  EXTTDV25
           LABEL RECORD STANDARD
           BLOCK CONTAINS 0 RECORDS
           RECORDING MODE IS F
           RECORD CONTAINS 44 CHARACTERS
           DATA RECORD IS FD-EXTTDV25
           .
       01  FD-EXTTDV25                     PIC X(44).

      *--------------------------------------------------------------*  00210008
       WORKING-STORAGE                     SECTION.                     00160004
      *--------------------------------------------------------------*  00210008
      * DECLARACAO DAS VARIAVEIS ESPELHO DOS ARQUIVOS
      *--------------------------------------------------------------*  00210008
           COPY B112A025.
           COPY B112CR25.
           COPY B112DV25.

      *--------------------------------------------------------------*  00210008
      * VARIAVEIS DE FILE STATUS
      *--------------------------------------------------------------*  00210008
           COPY VARERRO.

      *--------------------------------------------------------------*  00210008
      * VARIAVEIS DE FILE STATUS
      *--------------------------------------------------------------*  00210008
       01  FS-EXTTA025                     PIC X(02).
           88 SUCESSO-A025                         VALUE '00'.
           88 FIM-ARQ-A025                         VALUE '10'.

       01  FS-EXTTCR25                     PIC X(02).
           88 SUCESSO-CR25                         VALUE '00'.
           88 FIM-ARQ-CR25                         VALUE '10'.

       01  FS-EXTTDV25                     PIC X(02).
           88 SUCESSO-DV25                         VALUE '00'.
           88 FIM-ARQ-DV25                         VALUE '10'.

      *--------------------------------------------------------------*  00210008
      * COLOCAR AS MENSAGEMS EM VARIAVEIS EH UMA BOA PRATICA
      *--------------------------------------------------------------*  00210008
       77  WS-MSG01                        PIC X(60)
                                                   VALUE
           'ERRO ABERTURA EXTTA025'.
       77  WS-MSG02                        PIC X(60)
                                                   VALUE
           'ERRO ABERTURA EXTTCR25'.
       77  WS-MSG03                        PIC X(60)
                                                   VALUE
           'ERRO ABERTURA EXTTDV25'.
       77  WS-MSG04                        PIC X(60)
                                                   VALUE
           'ERRO LEITURA EXTTA025'.
       77  WS-MSG05                        PIC X(60)
                                                   VALUE
           'ERRO GRAVACAO EXTTCR25'.
       77  WS-MSG06                        PIC X(60)
                                                   VALUE
           'ERRO GRAVACAO EXTTDV25'.
       77  WS-MSG07                        PIC X(60)
                                                   VALUE
           'ERRO FECHAMENTO EXTTA025'.
       77  WS-MSG08                        PIC X(60)
                                                   VALUE
           'ERRO FECHAMENTO EXTTCR25'.
       77  WS-MSG09                        PIC X(60)
                                                   VALUE
           'ERRO FECHAMENTO EXTTDV25'.

      *--------------------------------------------------------------*  00210008
      * VARIAVEIS DE TRABALHO
      *--------------------------------------------------------------*  00210008
       77  WS-CTLIDO                       PIC 9(04)    COMP.           00210208
       77  WS-CTGRAV-CR25                  PIC 9(04)    COMP.           00210308
       77  WS-CTGRAV-DV25                  PIC 9(04)    COMP.           00210308
      *--------------------------------------------------------------*  00210008
      * VARIAVEIS COM MASCARA DE EDICAO (FORMATADAS)
      *--------------------------------------------------------------*  00210008
       77  WS-CTLIDO-F                     PIC ZZZ9.                    00210208
       77  WS-CTGRAV-CR25-F                PIC ZZZ9.                    00210308
       77  WS-CTGRAV-DV25-F                PIC ZZZ9.                    00210308
      *--------------------------------------------------------------*  00211007
       PROCEDURE                           DIVISION.                    00220004
      *--------------------------------------------------------------*  00230004
       0000-MAIN.
           PERFORM 1000-INICIALIZAR
           PERFORM 2000-PROCESSAR
                   UNTIL FIM-ARQ-A025
           PERFORM 3000-TERMINO
           GOBACK
           .
      *
       1000-INICIALIZAR.
           MOVE 0                          TO WS-CTLIDO
           MOVE 0                          TO WS-CTGRAV-CR25
           MOVE 0                          TO WS-CTGRAV-DV25
      *
           OPEN INPUT EXTTA025
      *
           IF NOT SUCESSO-A025
              MOVE WS-MSG01                TO WS-MSG
              MOVE FS-EXTTA025             TO WS-FS
              PERFORM 9000-ERRO
           END-IF
      *
           OPEN OUTPUT EXTTCR25
      *
           IF NOT SUCESSO-CR25
              MOVE WS-MSG02                TO WS-MSG
              MOVE FS-EXTTCR25             TO WS-FS
              PERFORM 9000-ERRO
           END-IF

           OPEN OUTPUT EXTTDV25

           IF NOT SUCESSO-DV25
              MOVE WS-MSG03                TO WS-MSG
              MOVE FS-EXTTDV25             TO WS-FS
              PERFORM 9000-ERRO
           END-IF
      *
           PERFORM 1500-LER-EXTTA025
           .
      *
       1500-LER-EXTTA025.
           READ EXTTA025 INTO REG-EXTTA025
      *
           IF SUCESSO-A025
              ADD 1 TO WS-CTLIDO
           ELSE
              IF NOT FIM-ARQ-A025
                 MOVE WS-MSG04             TO WS-MSG
                 MOVE FS-EXTTA025          TO WS-FS
                 PERFORM 9000-ERRO
              END-IF
           END-IF
           .
      *
       2000-PROCESSAR.
           IF B112A025-SALDO >= 0
              MOVE REG-EXTTA025            TO REG-EXTTCR25
              WRITE FD-EXTTCR25 FROM REG-EXTTCR25
      *
              IF NOT SUCESSO-CR25
                 MOVE WS-MSG05             TO WS-MSG
                 MOVE FS-EXTTCR25          TO WS-FS
                 PERFORM 9000-ERRO
              END-IF
      *
              ADD 1 TO WS-CTGRAV-CR25
           ELSE
              MOVE REG-EXTTA025            TO REG-EXTTDV25
              WRITE FD-EXTTDV25 FROM REG-EXTTDV25
      *
              IF NOT SUCESSO-DV25
                 MOVE WS-MSG06             TO WS-MSG
                 MOVE FS-EXTTDV25          TO WS-FS
                 PERFORM 9000-ERRO
              END-IF
      *
              ADD 1 TO WS-CTGRAV-DV25
           END-IF
      *
           PERFORM 1500-LER-EXTTA025
           .
      *
       3000-TERMINO.
           MOVE WS-CTLIDO                  TO WS-CTLIDO-F
           MOVE WS-CTGRAV-CR25             TO WS-CTGRAV-CR25-F
           MOVE WS-CTGRAV-DV25             TO WS-CTGRAV-DV25-F

      *
      *                      1         2         3         4         5
      *             12345678901234567890123456789012345678901234567890
           DISPLAY "=================================================="
           DISPLAY "TOTAL DE REGISTROS LIDOS.........: "
                                           WS-CTLIDO-F
           DISPLAY "TOTAL DE REGISTROS GRAVADOS CR25.: "
                                           WS-CTGRAV-CR25-F
           DISPLAY "TOTAL DE REGISTROS GRAVADOS DV25.: "
                                           WS-CTGRAV-DV25-F
           DISPLAY "=================================================="
      *
           CLOSE EXTTA025
      *
           IF NOT SUCESSO-A025
              MOVE WS-MSG07                TO WS-MSG
              MOVE FS-EXTTA025             TO WS-FS
              PERFORM 9000-ERRO
           END-IF
      *
           CLOSE EXTTCR25
      *
           IF NOT SUCESSO-CR25
              MOVE WS-MSG08                TO WS-MSG
              MOVE FS-EXTTCR25             TO WS-FS
              PERFORM 9000-ERRO
           END-IF

      *
           CLOSE EXTTDV25
      *
           IF NOT SUCESSO-DV25
              MOVE WS-MSG09                TO WS-MSG
              MOVE FS-EXTTDV25             TO WS-FS
              PERFORM 9000-ERRO
           END-IF

           DISPLAY "=================================================="
           DISPLAY "==          TERMINO NORMAL DO P112NX03          =="
           DISPLAY "=================================================="
           .
           COPY ROTERRO.
